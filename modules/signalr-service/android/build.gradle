// Ensure plugin repositories are available when building standalone
buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath('com.android.tools.build:gradle:8.7.3')
    classpath('org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.21')
  }
}

group = 'expo.modules.signalrservice'
version = '0.7.2'

def expoModulesCorePlugin = new File(
  providers.exec {
    workingDir(rootDir.parentFile)
    commandLine("node", "--print", "require.resolve('expo-modules-core/android/ExpoModulesCorePlugin.gradle')")
  }.standardOutput.asText.get().trim()
)
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()

apply plugin: 'com.android.library'

useCoreDependencies()
useExpoPublishing()

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 34)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 21)
      targetSdkVersion safeExtGet("targetSdkVersion", 34)
    }
  }
}

android {
  namespace "expo.modules.signalrservice"
  defaultConfig {
    versionCode 1
    versionName "0.7.2"
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = "17"
  }
}

dependencies {
  // SignalR Java client and RxJava3
  implementation 'com.microsoft.signalr:signalr:10.0.0-preview.7.25380.108'
  implementation 'io.reactivex.rxjava3:rxjava:3.1.8'
}
